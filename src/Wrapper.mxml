<?xml version="1.0"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
                       xmlns:s="library://ns.adobe.com/flex/spark"
                       showStatusBar="false"
                       width="400"
                       height="200"
                       preinitialize="onPreinitialize(event)"
                       creationComplete="onComplete(event)"
                       frameRate="60">
    <fx:Script><![CDATA[
        import mx.events.FlexEvent;

        protected function onPreinitialize(event:FlexEvent):void {
            // Center window on screen
            visible = true;
            center();
        }

        protected function onComplete(event:FlexEvent):void {
            inputUrl.text = "https://github.com/olinkirkland/Generations/raw/release/out/production/Generations/Main.swf";
            buttonClear.enabled = buttonRun.enabled = File.applicationStorageDirectory.resolvePath("Main.swf").exists;
            buttonDownload.enabled = !buttonClear.enabled;
            labelInfo.text = buttonRun.enabled ? "Click run to start" : "No local file";
        }

        private function loadSwf():void {
            SoundMixer.soundTransform = new SoundTransform(0);

            var context:LoaderContext = new LoaderContext();
            context.allowLoadBytesCodeExecution = true;
            context.allowCodeImport = true;

            var swfFile:File = File.applicationStorageDirectory.resolvePath('Main.swf');
            var fileStream:FileStream = new FileStream();
            fileStream.open(swfFile, FileMode.READ);

            var swfBytes:ByteArray = new ByteArray();
            fileStream.readBytes(swfBytes);
            fileStream.close();

            swfLoader.loaderContext = context;
            swfLoader.source = swfBytes;
            swfLoader.load();

            splash.visible = false;

            swfLoader.addEventListener(Event.COMPLETE, function (event:Event):void {
                try {
                    width = swfLoader.content["measuredWidth"];
                    height = swfLoader.content["measuredHeight"];
                } catch (error:Error) {
                    // No dimensions in loader
                }
                center();
            });
        }

        private function onClickButtonSubmit(event:MouseEvent):void {
            inputUrl.enabled = false;
            buttonDownload.enabled = !buttonClear.enabled;

            var urlStream:URLStream = new URLStream();

            urlStream.addEventListener(ProgressEvent.PROGRESS, function (event:ProgressEvent) {
                labelInfo.text = (event.bytesLoaded / 1000).toFixed(1) + " KB / " + (event.bytesTotal / 1000).toFixed(1) + " KB";
            });

            urlStream.addEventListener(Event.COMPLETE, function (event:Event) {
                var fileData:ByteArray = new ByteArray();
                urlStream.readBytes(fileData, 0, urlStream.bytesAvailable);
                fileStream.openAsync(file, FileMode.WRITE);
                fileStream.writeBytes(fileData, 0, fileData.length);
                fileStream.close();

                //inputUrl.enabled = true;
                focusManager.setFocus(inputUrl);
                buttonClear.enabled = buttonRun.enabled = File.applicationStorageDirectory.resolvePath("Main.swf").exists;

                labelInfo.text = "Click run to start";
            });

            urlStream.addEventListener(IOErrorEvent.IO_ERROR, function (event:IOErrorEvent) {
                //inputUrl.enabled = true;
                buttonDownload.enabled = true;
                focusManager.setFocus(inputUrl);

                labelInfo.text = "Error! Not a valid address";
            });

            var fileStream:FileStream = new FileStream();
            var file:File = File.applicationStorageDirectory.resolvePath("Main.swf");
            urlStream.load(new URLRequest(inputUrl.text));
        }

        private function onClickButtonLoad(event:MouseEvent):void {
            loadSwf();
        }

        private function onClickButtonClear(event:MouseEvent):void {
            File.applicationStorageDirectory.resolvePath("Main.swf").deleteFile();
            buttonClear.enabled = buttonRun.enabled = false;
            buttonDownload.enabled = true;
            labelInfo.text = "No local file";
        }

        private function onSwfLoaderComplete(event:Event):void {
            swfLoader.visible = swfLoader.includeInLayout = true;
        }

        protected function center():void {
            nativeWindow.x = (Capabilities.screenResolutionX - width) / 2;
            nativeWindow.y = (Capabilities.screenResolutionY - height) / 2;
        }
        ]]></fx:Script>

    <s:VGroup id="splash"
              width="100%"
              height="100%"
              horizontalAlign="center"
              verticalAlign="middle"
              padding="10" gap="10">

        <s:Label text="Generations" fontSize="24"/>

        <s:TextInput id="inputUrl"
                     width="100%" enabled="false"/>

        <s:HGroup width="100%" horizontalAlign="center">

            <s:Button id="buttonDownload"
                      label="Download"
                      click="onClickButtonSubmit(event)"/>

            <s:Button id="buttonRun"
                      label="Run"
                      click="onClickButtonLoad(event)"/>

            <s:Button id="buttonClear"
                      label="Delete"
                      click="onClickButtonClear(event)"/>
        </s:HGroup>

        <s:Label id="labelInfo"
                 width="100%"
                 textAlign="center"/>

    </s:VGroup>

    <s:SWFLoader id="swfLoader"
                 visible="false"
                 includeInLayout="false"
                 autoLoad="false"
                 width="100%"
                 height="100%"
                 complete="onSwfLoaderComplete(event)"
                 maintainAspectRatio="false"
                 trustContent="true"/>

</s:WindowedApplication>
